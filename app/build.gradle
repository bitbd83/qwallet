def isCI = hasProperty("isCI")
def isLightCI = hasProperty("isLightCI")
def includeFirebase = isCI || hasProperty("firebase")
def includeOffline = hasProperty("isCI")

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'org.jetbrains.kotlin.android.extensions'
apply plugin: "com.jaredsburrows.license"

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "org.walleth"

        minSdkVersion 15
        targetSdkVersion 28

        versionCode 455
        versionName "0.45.5"
        archivesBaseName = "Q-Wallet-$versionName"

        testInstrumentationRunner "org.walleth.infrastructure.AppReplacingRunner"
        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        warning 'InvalidPackage' // for OKIO
        warning 'MissingTranslation'
    }

    flavorDimensions "geth", "fire", "store", "connectivity"

    productFlavors {
        withGeth {
            dimension "geth"
        }

        noGeth {
            dimension "geth"
        }

        forFDroid {
            dimension "store"
            buildConfigField 'String', 'STORE', '"fdroid"'
        }

        forAmazon {
            dimension "store"
            buildConfigField 'String', 'STORE', '"amazon"'
        }

        forPlay {
            dimension "store"
            buildConfigField 'String', 'STORE', '"play"'
        }

        if (includeFirebase) {
            withFirebase {
                dimension "fire"
            }
        }

        noFirebase {
            dimension "fire"
        }

        online {
            dimension "connectivity"
        }

        offline {
            dimension "connectivity"
            applicationIdSuffix ".offline"
        }

    }

    applicationVariants.all { variant ->
        variant.resValue "string", "package_name", "${variant.properties.applicationId}"
    }

    android.variantFilter { variant ->
        def geth = variant.getFlavors().get(0).name
        def firebase = variant.getFlavors().get(1).name
        def store = variant.getFlavors().get(2).name
        def connectivity = variant.getFlavors().get(3).name

        if ((project.hasProperty("singleFlavor") && (store != 'forFDroid'))
                || ((store == 'forAmazon' || store == 'forPlay') && firebase == 'noFirebase')
                || (store == 'forFDroid' && firebase != 'noFirebase')
                || (connectivity == "offline" && !includeOffline)
                || (connectivity == 'offline' && (firebase == 'withFirebase' || geth == 'withGeth'))) {
            variant.setIgnore(true)
        }
    }

    // Ensure the no-op dependency is always used in JVM tests.
    configurations.all { config ->
        if (config.name.contains('UnitTest') || config.name.contains('AndroidTest')) {
            config.resolutionStrategy.eachDependency { details ->
                if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                    details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1"
    implementation 'androidx.multidex:multidex:2.0.1'
    androidTestImplementation 'androidx.multidex:multidex:2.0.1'
    androidTestImplementation 'androidx.multidex:multidex-instrumentation:2.0.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    withGethImplementation "org.ethereum:geth:1.8.8"

    implementation 'androidx.paging:paging-runtime:2.1.0'

    implementation "com.github.walleth.kethereum:functions:$kethereum_version"
    implementation "com.github.walleth.kethereum:model:$kethereum_version"
    implementation "com.github.walleth.kethereum:crypto:$kethereum_version"
    implementation "com.github.walleth.kethereum:bip32:$kethereum_version"
    implementation "com.github.walleth.kethereum:bip39:$kethereum_version"
    implementation "com.github.walleth.kethereum:bip39_wordlist_en:$kethereum_version"
    implementation "com.github.walleth.kethereum:bip44:$kethereum_version"
    implementation "com.github.walleth.kethereum:blockscout:$kethereum_version"
    implementation "com.github.walleth.kethereum:erc55:$kethereum_version"
    implementation "com.github.walleth.kethereum:eip155:$kethereum_version"
    implementation "com.github.walleth.kethereum:eip191:$kethereum_version"
    implementation "com.github.walleth.kethereum:erc681:$kethereum_version"
    implementation "com.github.walleth.kethereum:erc831:$kethereum_version"
    implementation "com.github.walleth.kethereum:erc1328:$kethereum_version"
    implementation "com.github.walleth.kethereum:extensions:$kethereum_version"
    implementation "com.github.walleth.kethereum:keccak_shortcut:$kethereum_version"
    implementation "com.github.walleth.kethereum:method_signatures:$kethereum_version"
    implementation "com.github.walleth.kethereum:networks:$kethereum_version"
    implementation "com.github.walleth.kethereum:etherscan:$kethereum_version"
    implementation "com.github.walleth.kethereum:rlp:$kethereum_version"
    implementation "com.github.walleth.kethereum:rpc:$kethereum_version"
    implementation "com.github.walleth.kethereum:wallet:$kethereum_version"
    implementation "com.github.walleth.kethereum:contract_abi_types:$kethereum_version"
    implementation "com.github.walleth.kethereum:keystore:$kethereum_version"

    implementation 'org.bouncycastle:bcprov-jdk15on:1.61'

    implementation "com.github.walleth:khex:0.6"

    implementation "com.github.walleth:KHardWareWallet:0.8"
    implementation "com.github.walleth.kethereum:crypto_impl_bouncycastle:$kethereum_version"

    implementation "com.github.walletconnect:kotlin-walletconnect-lib:0.8"

    implementation 'com.dmitrymalkovich.android:progress-fab:1.6'

    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'

    implementation 'com.github.ligi.tracedroid:lib:2.1'
    implementation 'com.github.ligi.tracedroid:supportemail:2.1'

    implementation 'com.github.ligi:KAXT:0.22'

    implementation 'com.github.ligi:KAXTUI:0.8'
    implementation 'com.github.ligi:thirdpartylicensedisplay:0.4'

    implementation 'com.androidadvance:topsnackbar:1.1.1'

    implementation('com.github.ligi:trezor-android:only_library_for_ethereum_tag_core210') {
        exclude group: 'com.android.support'
    }

    implementation 'com.google.protobuf:protobuf-java:3.7.1'

    implementation 'com.github.kenglxn.QRGen:android:2.5.0'
    implementation 'com.google.zxing:core:3.3.3'

    implementation 'com.jakewharton:process-phoenix:2.0.0'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'

    implementation "org.koin:koin-android:1.0.2"
    implementation "org.koin:koin-android-viewmodel:1.0.2"

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    implementation "com.chibatching.kotpref:kotpref:2.8.0"

    implementation 'com.squareup.moshi:moshi:1.8.0'

    implementation 'com.github.ligi:ExtraCompats:0.5'
    implementation 'com.github.ligi:KRoom:0.4'

    implementation 'androidx.preference:preference:1.0.0'

    implementation 'com.github.ligi:rootbeer:remove_native_workaround'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    // Kotlin + coroutines
    implementation 'androidx.work:work-runtime-ktx:2.0.1'

    implementation 'com.github.madrapps:pikolo:1.1.8'

    implementation 'androidx.room:room-runtime:2.0.0'
    kapt 'androidx.room:room-compiler:2.0.0'

    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'

    androidTestImplementation "com.google.truth:truth:$truth"

    testImplementation "org.mockito:mockito-core:$mockito_version"

    androidTestImplementation "org.mockito:mockito-core:$mockito_version"

    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.25.0'


    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.1'
    androidTestImplementation 'com.github.ligi:trulesk:0.28'

    testImplementation 'junit:junit:4.12'
    testImplementation "com.google.truth:truth:$truth"
    testImplementation 'org.threeten:threetenbp:1.3.8'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'

    if (includeFirebase) {
        withFirebaseImplementation "com.google.firebase:firebase-core:15.0.2"
        withFirebaseImplementation "com.google.firebase:firebase-crash:15.0.2"
        withFirebaseImplementation "com.google.firebase:firebase-messaging:15.0.2"
    }
}

if (isCI || isLightCI) {

    apply plugin: 'com.trevjonez.composer'
    composer {
        apkInstallTimeout 600
    }
    apply plugin: 'witness'
    apply from: 'witness.gradle'
}
if (includeFirebase) {
    apply plugin: 'com.google.gms.google-services'
}

licenseReport {
    generateHtmlReport = false
    generateJsonReport = true

    copyJsonReportToAssets = true
}

androidExtensions {
    experimental = true
}
