buildscript {
    ext {
        kotlin_version = '1.3.30'
        support_version = '28.0.0'
        firebase_version = '15.0.2'
        kethereum_version = '0.75.0'
        multidex_version = '1.0.3'
        arch_version = '1.1.1'
        room_version = '1.1.1'
        mockito_version = '2.27.0'
        truth = '0.42'
        kodein_version = '5.1.1'
    }

    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        gradlePluginPortal()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'de.mobilej.unmock:UnMockPlugin:0.7.3'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath 'com.github.trevjonez.composer-gradle-plugin:plugin:0.11.1'
        classpath files('app/libs/gradle-witness.jar')
        classpath 'com.google.gms:google-services:4.2.0'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
        classpath "com.jaredsburrows:gradle-license-plugin:0.8.42"
        classpath "com.github.plnice:canidropjetifier:0.4"
    }
}
task clean(type: Delete) {
    delete rootProject.buildDir
}


allprojects {
    repositories {
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url 'https://jitpack.io' }
        google()
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.plnice.canidropjetifier'

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

